-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
-- System chart
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
system_chart MONITORING_SYSTEM
  indexing
    about:        "This system facilitates the assertion, collection, archival, and reporting of assertions, logging messages, and statistics.";
    title:        "Monitoring system";
    author:       "Joseph Kiniry";
    copyright:    "Copyright (C) 1998-2009 Joseph R. Kiniry";
    organisation: "Caltech, Radboud University Nijmegen, UCD";
    date:         "1 July 2009";
    version:      "2.0";
  cluster EVENTS description "Facilities for registering an interest in, \
    \ and recording of, important events (errors, warnings, or just \
    \ informational) that occur during the operation of the system/framework \
    \ or overlying applications."
  cluster PROCESSING description "Facilities for processing and recording events."
end

-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
-- Cluster charts
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
cluster_chart EVENTS
  class ASSERTION description "Represents a single type of important \
    \ predicate that the system should validate.  Assertion failures result \
    \ in everything from messages or statistics being logged to system failure." 
  class EVENT description "Represents a single event to be recorded (or that \
    \ has been recorded) in the event-log.  The event includes a source, \
    \ description, importance, and time, among other things."
  class LEVEL description "Represents the importance level of a particular event."
  class LOG description "Manages the storage and retrieval of events that \
    \ are reported by various parts of the system framework and/or the \
    \ application built on top of it."
  class MESSAGE description "Represents a single textual message that needs \
    \ to be displayed or recorded."
  class STATISTIC description "Represents a statistic that can be \
    \ monitored in the monitoring system.  Each statistic has its own unique \
    \ ID (node-unique, not necessarily globally unique), a description, units, \
    \ and other useful information that is represented in this class."
  class TYPE description "Represents the type of a particular event. The set \
    \ of core event types each have a name and a description."
end

cluster_chart PROCESSING
  class CHECKER description "Checks the state of the system for validity \
    \ against particular assertions." 
  class COLLECTOR description "Performs the collection of particular statistics \
    \ during the operation of a program."
  class LOGGER description "Logs particular messages during the operation of \
    \ a program."
  class TUNER description "'Tune' the monitoring system to collect different \
    \ information."
end

-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
-- Classes in Cluster EVENTS
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
class_chart ASSERTION
indexing
  incluster: "EVENTS";
  created: "2000-07-19 Joseph Kiniry";
explanation
  "Represents a single type of important predicate that the system should \
 \ validate.  Assertion failures result in everything from messages or \
 \ statistics being logged to system failure."
end
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
class_chart EVENT
indexing
  incluster: "EVENTS";
  created: "2000-07-06 Joseph Kiniry";
explanation
  "Represents a single important event of any kind. The event includes a \
 \ source, description, importance, and time, among other things."
query
  "What is the source system of this event?", 
  "What is the source component of this event?",
  "What type of event is this?", 
  "When was this event generated?", 
  "What is the description of this event?", 
  "How important is this event?", 
  "What is a printable representation of this event?", 
  "Is this event equal to another one?",
command
  "Create a new event with the following specification."
constraint
  "All fields of event are set only once, at construction time.",
  "All fields of event must be specified; none may be left blank.",
  "Two events are considered to be identical if all fields of both events \
       \ have identical values.",
  "Events cannot be cloned."
end
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
class_chart LEVEL
indexing
  incluster: "EVENTS";
  created: "2000-09-19 Joseph Kiniry";
explanation
  "Represents the importance level of a particular event."
query
  "What is the level set to?",
  "What is the minimum/maximum level?"
command
  "Set the level to a value.", 
  "How does this level compare with another?"
constraint
  "Levels have upper and lower bounds, thus all level assignments must be \
 \ bounded appropriately."
end
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
class_chart LOG
indexing
  incluster: "EVENTS";
  created: "2000-07-19 Joseph Kiniry";
explanation
  "Manages the storage and retrieval of events that are reported by \
 \ various parts of the system framework and/or the application built \
 \ on top of it."
query
  "Which events meet a specified criteria?", 
  "What is the list of all event sources?"
command
  "Store event.", 
  "Delete event.", 
  "Clear event log.", 
  "Delete events meeting specified criteria.", 
  "Export event log to a file.", 
  "Import event log from a file."
end
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
class_chart MESSAGE
indexing
  incluster: "EVENTS";
  created: "2000-07-19 Joseph Kiniry";
explanation
  "Represents a single textual message that needs to be displayed or recorded."
query
  "What textual message does this message contain?"
command
  "Set textual message."
constraint
  "Textual message must be valid."
end
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
class_chart STATISTIC
indexing
  incluster: "EVENTS";
  created: "2000-07-06 Joseph Kiniry";
explanation
  "This class represents a statistic that can be monitored in the \
 \ monitoring system. Each statistic has its own unique ID (node-unique, \
 \ not necessarily globally unique), a description, units, and other \
 \ useful information that is represented in this class."
query
  "What is the unique ID of this statistic?", 
  "What are its units?", 
  "What is its default scaling factor?", 
  "What is its default start value?", 
  "What is its default increment?", 
  "What is its default decrement?"
command
  "Create a new statistic by specifying its units, scaling factor, \
 \ default start value, default increment and default decrement."
constraint
  "Unique ID must be unique across all statistics on a node.",
  "All static properties must be specified at, and cannot be changed \
 \ after, construction time."
end
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
class_chart TYPE
indexing
  incluster: "EVENTS";
  created: "2000-07-19 Joseph Kiniry";
explanation
  "Represents the type of a particular event. The set of core event types \
 \ each have such a name and a description."
query
  "What is this types current value?",
  "Is this type equivalent to another one?"
command
  "Set the value of this type."
constraint
  "Types must have some legal value."
end

-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
-- Classes in Cluster PROCESSING
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
class_chart CHECKER
indexing
  incluster: "PROCESSING";
  created: "2000-07-19 Joseph Kiniry";
explanation
  "Checks the state of the system for validity against particular assertions."
query
  "Is this assertion valid?"
end
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
class_chart COLLECTOR
indexing
  incluster: "PROCESSING";
  created: "2000-07-19 Joseph Kiniry";
explanation
  "Performs the collection of particular statistics during the operation of \
 \ a program.  Individual statistics can be registered for collection, and \
 \ may be activated or deactivated. The collector can cache statistic \
 \ collection in memory, and can also write statistics and logging information \
 \ to a long-term storage device for subsequent analysis."
query
  "What is the current value for specific statistic?",
  "Is this statistic active?"
command
  "Report on a particular statistic.",
  "Report on all statistics.", 
  "Increment a statistic by a specified value.", 
  "Increment a statistic by the default value.", 
  "Decrement a statistic by a specified value.", 
  "Decrement a statistic by the default value.", 
  "Reset a statistic to the default start value.", 
  "Set a statistic to a specific value.",
  "Register this statistic.",
  "Activate this statistic.",
  "Deactive this statistic"
constraint
  "A newly registered statistic is deactivated by default.",
  "No two statistics can be registered with the same unique ID."
end
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
class_chart LOGGER
indexing
  incluster: "PROCESSING";
  created: "2000-07-19 Joseph Kiniry";
explanation
  "Performs the collection of logging messages (events) during the operation \
 \ of a program."
query
  "What is the complete current log?", 
  "What was the last message logged?"
command
  "Clear the log.", 
  "Save the log.",
  "Close the log."
constraint
  "none"
end
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
class_chart TUNER
indexing
  incluster: "PROCESSING";
  created: "2000-07-19 Joseph Kiniry";
explanation
  "'Tune' the monitoring system to collect specific types and levels of \
 \ system statistics, messages, and assertions."
query
  "Is a certain level enabled/disabled?", 
  "Is a certain type enabled/disabled?",
  "What types are currently registered?"
command
  "Turn on/off assertions of a particular type.", 
  "Turn on/off assertions above/below a particular level."
constraint
  "Levels have a specified range.",
  "All level assignments must be within the specified range.",
  "Event types must be well-defined strings."
end
