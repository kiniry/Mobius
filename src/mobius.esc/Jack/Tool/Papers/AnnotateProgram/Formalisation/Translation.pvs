% Main theorem: translation steps 1, 2 and 3 result in new program
% whose execution simulates original program
Translation [CP, Name : TYPE+] : THEORY
BEGIN

ASSUMING 

    IMPORTING ValProp[Name]

    CP_is_enumerable : ASSUMPTION
      EXISTS (f : [CP -> nat]) : injective?(f)

    infinite_namespace : ASSUMPTION
      is_infinite({n : Name | TRUE})

    IMPORTING VariableNames[CP, Name]

    name_of_exists_annot_gen : ASSUMPTION
      EXISTS (f : [mp : (wf_and_complete_MP) -> [CP -> (legal_names(mp))]]) : 
        FORALL(mp : (wf_and_complete_MP)) : 
          injective?(f(mp))

    IMPORTING InlineVariableNames[Name]

    name_of_exists_inline : ASSUMPTION
      EXISTS (f : [p : Program -> [Method -> (legal_names(p))]]) :
        FORALL(p : Program) :
          injective?(f(p))

    IMPORTING MVA[CP, Name]

    CP_not_only_halted : ASSUMPTION
      EXISTS(cp : CP) : NOT cp = halted

ENDASSUMING

  IMPORTING CompleteMonitorProgramPropertiesPreservation[CP, Name],
            AnnotateProgramPropertiesPreservation[CP, Name],
            CompleteMonitor[CP, Name],
            CorrectnessAnnProgram[CP, Name],
            CorrectnessInlineProgram[Name]

  mp : VAR (wf_and_partial_MP)

  translate(mp) : Program = inline_program(ann_program(complete_MP(mp)))

  APStateToPartialAStore(mp : (wf_and_partial_MP)) : [APState -> Store] =
    APStateToAStore(complete_MP(mp))

  APStateToAStore_unchanged_by_completion : LEMMA % :-)
    FORALL(mp : (wf_and_partial_MP), sAP : APState) :
      APStateToPartialAStore(mp)(sAP) =  
        LAMBDA (n : Name) : 
          IF var_names(vdsA(mva(mp)))(n)
          THEN gvs(sAP)(n)
          ELSE Bottom
          ENDIF

  % legal_Program_AP: see comment in PreservationProperties
  main : THEOREM % :-)
    FORALL (main : Name, arg : int)(root_object : (defined?))
           (sMP : MPState) :
    well_behaved_MVA(mva(mp))(APStateToPartialAStore(mp)) IMPLIES
    legal_Program_AP(ann_program(complete_MP(mp))) IMPLIES
    contains_no_method_call_in_sets_in_program(program(mp)) IMPLIES
    wf_set_stmts(mp) IMPLIES
      run_monitored_program(mp)(main, arg)(root_object)(sMP) IMPLIES
        EXISTS(sAP : APState) :
          run_annotated_program(translate(mp))(main, arg)(root_object)(sAP) AND
            EXISTS (R : [MPState, APState -> boolean]) : R(sMP, sAP)   

END Translation
