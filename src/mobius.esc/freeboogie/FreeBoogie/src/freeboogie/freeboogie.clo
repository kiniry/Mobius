NAME::
  FbCli
  :"FreeBoogie is a verifier for Boogie programs. The valid options are:"

ARGS::
  Files :{} :{file-list} :[mustExist,canBeDir="false",between="",allowMultiple="false"]
    :"Configure the list of files to check."

  SeriesParallelCheck :{"--series-parallel-check", "-spc"}
    :[allowArg="false"]
    :"Checks if the flowgraphs of the given implementations are 
      series--parallel. A (two terminal) series--parallel graph is either
      an edge or the series composition of two series--parallel graphs or
      the parallel composition of two series--parallel graphs. For the
      purposes of this check a unique exit node is added to the graph,
      and all the real exit nodes go to this one."
 
  SkipHavocLoopVariables :{"--skip-havoc", "-sh"} :[allowArg="false"]
    :"Do not havoc variables changed in a loop upon entering the loop.
      This is unsound if loops are cut later."
  SkipAskProver :{"--skip-prover", "-sp"} :[allowArg="false"]
    :"Do not ask the prover if the VC is valid."
  RunToHavocLoopVariables :{"--do-havoc", "-dh"} :[allowArg="false"]
    :"Skip all the phases after havocking loop variables."
  RunToCutLoops :{"--do-cut-loops", "--do-cut", "-dc"} : [allowArg="false"]
    :"Skip all the phases after cutting backedges in the flowgraph."
  CheckOnly :{"--check-only"}: {string}
    :"Check only the procedures whose name match the given regular
      expression."

  PassivatorOpt :{"--passivator"} :{enum}
    :[choices="OPTIM,ESCJAVA", default="optim"]
    :"The optim passivator introduces a minimum number of new
      variables. The escjava algorithm can be selected for doing
      performance measurements: It should always be worse."
  VcMethodOpt :{"--vc-method", "-vm"} :{enum}
    :[choices="wp(WP),sp(SP)", default="wp"]
    :"Configure which method should be used for building a VC from the
      transformed Boogie."
  AssumeAsserts :{"--assume-asserts", "-aa"}
    :"If set, then 'assert X' is treated as 'assert X; assume X'."
  ProverOpt :{"--prover", "-p"} :{enum} 
    :[choices="SIMPLIFY,YESMAN", default="simplify"]
    :"Configure which prover to use. The prover 'yes' can be useful
      for debugging (and for doing demos) because it always says that
      the code is OK."
  ProverCommandLineOpt :{"--prover-command-line", "-pcl"} :{string}
    :"Configure the command line used to start a prover. The default
      depends on the selected prover."
  BoogieVersionOpt :{"--boogie-version", "-bv"} :{enum}
    :[choices="1(ONE),2(TWO)", default="2"]
    :"Configure the version of the Boogie language."

  ReportOn :{"--report-on", "--out-categories", "-oc"} :{enum-list}
    :[choices="MAIN,FOO",default="MAIN"]
    :"Configure what type of information should go to stdout.
      By default all categories are disabled. Errors go to stderr."
  ReportLevel :{"--report-level", "--out-level", "-ol"} :{enum}
    :[choices="DEBUG,VERBOSE,NORMAL,QUIET", default="normal"]
    :"Configure how much information goes to stdout."
  LogFile :{"--log-file", "-lf"} :{file} 
    :[canBeDir="false",default="freeboogie.log"]
    :"Configure where logging should be done. If the file does not exist
      it is created; if the file does exist the new log messages are appended."
  LogCategories :{"--log-categories", "-lc"} :{enum-list} 
    :[choices="MAIN,VCGEN,PROVER,STATS,BACKEND"]
    :"Configure what type of information should go in the log file. 
      By default all categories are disabled."
  LogLevel :{"--log-level", "-ll"} :{enum} 
    :[choices="INFO,WARNING,ERROR", default="warning"]
    :"Configure how much information should be logged."
  DumpIntermediateStages :{"--dump-intermediate-stages", "-dis"} :{file}
    :[canExist="false"]
    :"Specify a directory where to dump the result of each transformation.
      The dump includes a Boogie program, its symbol table, and its
      flowgraphs."

  Help :{"--help", "-h"}
    :"Display this message."

  // These cannot appear on the command line.
  MakeHavoc :{} :[default="true"]
  CutLoops :{} :[default="true"]
  DesugarCalls :{} :[default="true"]
  DesugarHavoc :{} :[default="true"]
  DesugarSpec :{} :[default="true"]
  Passivate :{} :[default="true"]
  DoVcGen :{} :[default="true"]

FORMAT::
  (Option | Files)* | Help;

WHERE::
  Option:
    ActionOption |
    StageOption |
    ConfigOption |
    OutputOption;

  ActionOption: SeriesParallelCheck;
  StageOption:
    SkipHavocLoopVariables |
    SkipAskProver |
    RunToHavocLoopVariables |
    RunToCutLoops |
    CheckOnly;
  ConfigOption:
    PassivatorOpt |
    VcMethodOpt |
    AssumeAsserts |
    ProverOpt |
    ProverCommandLineOpt |
    BoogieVersionOpt;
  OutputOption: 
    LogFile |
    LogCategories |
    LogLevel |
    ReportOn |
    ReportLevel |
    DumpIntermediateStages;

OVERRIDES::
  {$(ProverOpt).toLowerCase().equals("simplify") && !$(ProverCommandLineOpt?)} -> 
    ProverCommandLineOpt := {"z3 -si"};
